# mmc/scsi etc
setenv devtype mmc
# The device number of the given type
setenv devnum 0
# The ubuntu-seed partition
setenv seedpart 1
# The ubuntu-boot partition
setenv bootpart 2

setenv kernel_name kernel.img
setenv ramdisk_name initrd.img
setenv kernel_addr_r  0x40040000
setenv dtbo_addr_r    0x4e000000
setenv ramdisk_addr_r 0x50000000

setenv overlays "uart2"

echo "Loading ubuntu-seed configuration..."
load ${devtype} ${devnum}:${seedpart} ${scriptaddr} uboot/ubuntu/boot.sel
env import ${scriptaddr}

echo "Snapd boot mode: ${snapd_recovery_mode}"

if test "${snapd_recovery_mode}" = "run"
then
    echo "Loading ubuntu-boot configuration..."
    load ${devtype} ${devnum}:${bootpart} ${scriptaddr} uboot/ubuntu/boot.sel
    env import ${scriptaddr}
    setenv kernel_bootpart "${bootpart}"
    if test "${kernel_status}" = "try"
    then
        echo "Trying to boot with ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_try_kernel}/"
    else
        echo "Using ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_kernel}/"
    fi
else
    # Prefix within the seed partiion to the boot files
    setenv prefix "systems/${snapd_recovery_system}/kernel/"
    setenv kernel_bootpart "${seedpart}"
fi

setenv console "console=ttyS0,115200"
setenv bootargs "${console} earlycon rw snapd_recovery_mode=${snapd_recovery_mode} snapd_recovery_system=${snapd_recovery_system} ${bootargs}"

echo "Loading ${prefix}${kernel_name}..."
load ${devtype} ${devnum}:${kernel_bootpart} ${kernel_addr_r} "${prefix}${kernel_name}"
echo "Loading ${prefix}${ramdisk_name}..."
load ${devtype} ${devnum}:${kernel_bootpart} ${ramdisk_addr_r} "${prefix}${ramdisk_name}"
setenv ramdisk_size $filesize

fdt addr ${fdtcontroladdr}

for dtbo in ${overlays}; do
    setenv dtbo_file "overlays/${dtbo}.dtbo"
    if test -e ${devtype} ${devnum}:${seedpart} "${dtbo_file}"; then
        echo "Loading device tree overlay ${dtbo_file}..."
        load ${devtype} ${devnum}:${seedpart} ${dtbo_addr_r} "${dtbo_file}"
        fdt apply ${dtbo_addr_r}
    else
        echo "Skipping device tree overlay ${dtbo_file} (not found)"
    fi
done

booti ${kernel_addr_r} ${ramdisk_addr_r}:${ramdisk_size} ${fdtcontroladdr}
